plugins {
	id "dev.architectury.loom" version "1.7-SNAPSHOT"
	id "maven-publish"
}

base.archivesName = "${mod_name}-forge"
version = "${mod_version}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(17)

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

def generatedResources = file("src/generated")

sourceSets {
	main {
		resources.srcDir generatedResources
	}
}

loom {
	silentMojangMappingsLicense()

	forge {
		mixinConfigs("${mod_id}.mixins.json")
		accessTransformer(file("src/main/resources/META-INF/accesstransformer.cfg"))
	}

	runs {
		data {
			data()
			programArgs "--all", "--mod", "${mod_id}"
			programArgs "--output", generatedResources.absolutePath
		}
	}
}

repositories {
	maven {
		url = "https://www.cursemaven.com"
		content {
			includeGroup("curse.maven")
		}
	}
	maven {
		url = "https://maven.parchmentmc.org"
		content {
			includeGroup("org.parchmentmc.data")
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-${project.minecraft_version}:${project.parchment_mappings}@zip")
	}
	forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"
	modCompileOnly("curse.maven:lootr-361276:5636598")
//	modRuntimeOnly("curse.maven:async-locator-625069:4603343")
//	modCompileOnly("curse.maven:dungeon-crawl-324973:4612224")
//	modCompileOnly("curse.maven:beans-backpacks-946775:5368249")
//	modRuntimeOnly("curse.maven:lootr-361276:5636598")
//	modRuntimeOnly("curse.maven:quark-243121:5594847")
//	modRuntimeOnly("curse.maven:zeta-968868:5597406")
	modRuntimeOnly("curse.maven:yungs-better-dungeons-510089:5271360")
	modRuntimeOnly("curse.maven:yungs-api-421850:5769971")
}

tasks.named('processResources', ProcessResources).configure {
	var replaceProperties = [
			minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
			forge_version: forge_version, forge_version_range: forge_version_range,
			loader_version_range: loader_version_range,
			mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
			mod_author: mod_author, mod_description: mod_description, mod_icon: mod_icon, credits: credits
	]

	inputs.properties replaceProperties

	filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
		expand replaceProperties + [project: project]
	}}

tasks.named('jar', Jar).configure {
	manifest {
		attributes([
				"Specification-Title": mod_name,
				"Specification-Vendor": mod_author,
				"Specification-Version": "1", // We are version 1 of ourselves
				"Implementation-Title": project.name,
				"Implementation-Version": project.jar.archiveVersion,
				"Implementation-Vendor": mod_author,
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
	options.release = 17
}